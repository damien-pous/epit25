From epit Require Export setoids.

(** * Bisimulation and coinductive types in the Rocq proof assistant
    Course 1: Formalizing initial algebras and final coalgebras---towards the need for quotients
*)

(** * Preliminaries: Setoids

  Setoids emulate quotients that mathematicians routinely use.
  A setoid is the data of a Type paired with an equivalence relation.
  Morphisms between setoids are maps preserving this equivalence.

  We take the file [setoids.v] as a black box providing support to work with them (albeit feel free to open it!).
  The module provides us with:
  - A structure of [Setoid]
  - Notations for its equivalence: [eqv], written [â‰¡] ("\equiv")
  - The notion of map between setoids: [X -eqv-> Y]
*)

(** * Part 1: Categories

    In this first course, we aim specifically at the formalization of initial algebras and final coalgebras. The shortest path therefore takes us through defining:
    - categories and functors
    - isomorphisms
    - inital/final objects
    - (co)algebras

*)

(** * 1. Categories

    We start by formalizing the bits of category theory we need to build up to initial algebras and final coalgebras.
    Importantly, notice that objects are captured as a Type, while the homset between two objects are a Setoid.
    Indeed, thinking of standard examples, arrows may be functions (we want to consider them extensionally),
    lambda termes (we want to consider them up-to Î²Îµ), etc...
    We package in the same structure both the data and the axioms.
*)

Structure Category :=
  {
    (* the collection of objects *)
    ob :> Type;
    (* for each pair of objects, the collection of morphisms
        (a [Setoid] rather than [Type], because we want to be
         able to specify how to compare morphisms for equality) *)
    hom:> ob -> ob -> Setoid;
    (* for each object [A] the identity morphism on [A] *)
    id: forall {A}, hom A A;
    (* for each triple of objects the composition operation on morphisms *)
    comp: forall {A B C}, hom B C -> hom A B -> hom A C;

    (* composition should preserve morphism equivalence
        intuitively: [forall f f', fâ‰¡f' -> forall g g', gâ‰¡g' -> gâˆ˜f â‰¡ g'âˆ˜f'] *)
    comp_eqv:: forall {A B C}, Proper (eqv ==> eqv ==> eqv) (@comp A B C);

    (* identity is a neutral element on the left  *)
    idl: forall {A B} f, @comp A A B f id â‰¡ f;
    (* and on the right  *)
    idr: forall {A B} f, @comp B A A id f â‰¡ f;
    (* composition is associative *)
    compA: forall {A B C D} f g h, @comp A B D (@comp B C D h g) f â‰¡ @comp A C D h (@comp A B C g f)
  }.

(** thanks to the two coercions [:>] in the first two lines,
    a category [ð‚] can automatically be casted 
    - into a type (its type of objects), so that we can write
    [forall A B C: ð‚] to quantify over objects, rather than [forall A B C: ob ð‚]
    - into a function with two arguments (its type of morphisms), so that we can write
    [f: ð‚ A B] instead of [f: hom ð‚ A B]
    (below we actually define a notation so that we can also write just [f: A ~> B])
 *)

(** we declare many arguments as implicit 
    for instance, [id] formally has two arguments:
    the category [ð‚] of which it belongs to, and the object [A] which is both its source and target
    those can be inferred automatically most of the time, by the following command we declare them as implicit.
 *)
Arguments id {_ _}.
(** similarly, composition has six arguments (the category, the three objects, the two morphisms)
    we declare the first four as implicit since so that it suffices to provide the two morphisms *)
Arguments comp {_ _ _ _}.

(** standard notations for composition and homsets *)
Notation "g âˆ˜ f" := (comp g f).
Notation "A ~> B" := (hom _ A B) (at level 99, B at level 200, format "A  ~>  B").


(** We can already toy with the structure by defining a few categories.
   Note that [Program] allows you to only fill in explicitely the data in the definition of the structure.
   It will try to solve the properties automatically, and will provide them to you as obligations to solve interactively otherwise.
 *)
Section example_categories.

  (** The category with a single object, and a single morphism. *)
  Program Definition UNIT: Category :=
    {|
      ob             := unit;
      hom _ _        := unit;
      id _           := tt;
      comp _ _ _ _ _ := tt;
    |}.
  Next Obligation. by destruct f. Qed.
  Next Obligation. by destruct f. Qed.

  (** Important for us: the category of types and functions *)
  Program Definition TYPES: Category :=
    {|
      ob := Type;
      hom A B := eq_setoid (A -> B);
      id _ := fun x => x;
      comp _ _ _ f g := fun x => f (g x);
    |}.

  (** ** Exercise
    Define the category REL, whose objects are [Type]s and morphisms are relations.
  *)
    Fail Program Definition REL: Category := {|  |}.

  (** ** Exercise
    Given a Type [A] and a preorder [R] on [A], define the category PRE whose objects are [A]s and morphisms x -> y if and only if x <= y.
    Hints:
    - The standard library provides the [PreOrder] structure.
    - Warning: you may have to define your own setoid.
      If so, you can do so using the following constructor:
        Setoid.build : âˆ€ (sort : Type) (eqv : relation sort), Equivalence eqv â†’ Setoid
  *)
    Fail Program Definition PRE {A R} (PR : @PreOrder A R) : Category := {| |}.

  (** ** Exercise
    Define the [dual] category.
  *)
  Fail Program Definition dual (ð‚: Category): Category :=
    {| |}.

End example_categories.



(** * 2. Isomorphisms *)

(** ** epi/monos *)
Section epimono.
  Context {ð‚: Category}.
  Definition epi {A B: ð‚} (f: A ~> B) := forall C (g h: B ~> C), g âˆ˜ f â‰¡ h âˆ˜ f -> g â‰¡ h.
  Definition mono {A B: ð‚} (f: A ~> B) := forall C (g h: C ~> A), f âˆ˜ g â‰¡ f âˆ˜ h -> g â‰¡ h.
End epimono.

(** ** isomorphisms *)
Section iso.
  Context {ð‚: Category}.
  Record iso (A B: ð‚) :=
    {
      (* two morphisms *)
      fwd: A ~> B;
      bwd: B ~> A;
      (* and the proofs that they compose to the identity *)
      isoE : fwd âˆ˜ bwd â‰¡ id;
      isoE': bwd âˆ˜ fwd â‰¡ id
    }.
  (** like before, some arguments may remain implicit *)
  Arguments fwd {_ _}.
  Arguments bwd {_ _}.
  (** notations for the forward and backward morphisms *)
  Notation "i ^1"  := (fwd i) (at level 20).
  Notation "i ^-1" := (bwd i) (at level 20).
  Infix "â‰ƒ" := iso (at level 70).

  (** identity isomorphism *)
  Program Definition iso_refl A: A â‰ƒ A :=
    {| fwd := id; bwd := id |}.
  Next Obligation. intro. apply idl. Qed.
  Next Obligation. intro. apply idl. Qed.

  (** inverting an isomorphism *)
  Program Definition iso_sym A B (i: A â‰ƒ B): B â‰ƒ A :=
    {| fwd := i^-1; bwd := i^1 |}.
  Next Obligation. apply isoE'. Qed.
  Next Obligation. apply isoE. Qed.

  (** composition of isomorphisms *)
  Program Definition iso_trans A B C (i: A â‰ƒ B) (j: B â‰ƒ C): A â‰ƒ C :=
    {| fwd := j^1 âˆ˜ i^1; bwd := i^-1 âˆ˜ j^-1 |}.
  Next Obligation.
    Admitted.
  Next Obligation.
    Admitted.

  Lemma epi_iso A B (i: A â‰ƒ B): epi (i^1).
  Admitted.

  Lemma mono_iso A B (i: A â‰ƒ B): mono (i^1).
  Admitted.

End iso.
Notation "i ^1" := (fwd i) (at level 20).
Notation "i ^-1" := (bwd i) (at level 20).
Infix "â‰ƒ" := iso (at level 70).



(** * 3. Initial and final objects *)

Section universal.

  Context {ð‚: Category}.

  (** an object [I] is initial if for every other object [X],
      there exists a unique morphism [I~>X]  *)
  Record initial (I: ð‚) := {
      init_mor:> forall X, I ~> X;
      init_mor_unique: forall X (f: I ~> X), f â‰¡ init_mor X;
    }.
  (** in particular, any two parallel morphisms out of [I] must be equal *)
  Lemma init_unique I (i: initial I) X (f g: I ~> X): f â‰¡ g.
  Proof.
    by rewrite (init_mor_unique i _ f) (init_mor_unique i _ g).
  Qed.

  (** initial objects are all isomorphic *)
  Lemma initial_iso I I': initial I -> initial I' -> I â‰ƒ I'.
  Proof.
  Admitted.

  (** dually, an object [Z] is final if for every other object [X],
      there exists a unique morphism [X~>Z]
      Given what we do in the sequel, we found it more convenient to directly ask for [fin_unique] in the definition.
   *)
  Record final (Z: ð‚) := {
      fin_mor:> forall X, X ~> Z;
      fin_unique: forall X (f g: X ~> Z), f â‰¡ g;
    }.

  Lemma final_unique Z Z': final Z -> final Z' -> Z â‰ƒ Z'.
  Proof.
  Admitted.

End universal.

Section example_initial_final.

  (** ** Exercise
    Define the initial and final objects in TYPES
  *)

End example_initial_final.



(** * 4. Functors *)

Record Functor (ð‚ ðƒ: Category) :=
  {
    (* the action of the functor on objects (notation [F A] thanks to the coercion) *)
    app':> ð‚ -> ðƒ;
    (* the action of the functor on morphisms
        (no coercion, we have to write [app F f] for what is usually written [F f] in maths) *)
    app : forall {A B}, ð‚ A B -> ðƒ (app' A) (app' B);

    (* the action of the functor on morphisms should preserve morphism equivalence
        (i.e., [forall f f': A ~> B, f â‰¡ f' -> app F f â‰¡ app F f']) *)
    app_eqv:: forall {A B}, Proper (eqv ==> eqv) (@app A B);
    (* functors preserve identity morphisms *)
    app_id: forall {A}, app (id: A ~> A) â‰¡ id;
    (* functors preserve composition *)
    app_comp: forall {U V W} (f: U ~> V) (g: V ~> W), app (g âˆ˜ f) â‰¡ app g âˆ˜ app f;
  }.

(** The identity functor *)
Program Definition functor_id {ð‚}: Functor ð‚ ð‚ :=
  {|
    (* identity on objects *)
    app' A := A;
    (* and on morphisms *)
    app A B (f: A ~> B) := f;
  |}.
Next Obligation. by intros. Qed.

(** Composition of functors *)
Program Definition functor_comp {ð‚ ðƒ ð„} (G: Functor ðƒ ð„) (F: Functor ð‚ ðƒ): Functor ð‚ ð„ :=
  {|
    (* we just compose the two components *)
    app' A := G (F A);
    app A B (f: A ~> B) := app G (app F f);
  |}.
Next Obligation.
  intros* f g fg. by apply app_eqv, app_eqv.
Qed.
Next Obligation. cbn; intros. by rewrite 2!app_id. Qed.
Next Obligation. cbn; intros. by rewrite 2!app_comp. Qed.

(** Constant functor *)
Program Definition functor_constant {ð‚ ðƒ: Category} (D: ðƒ): Functor ð‚ ðƒ:=
  {|
    (* always the given object on objects *)
    app' _ := D;
    (* always the identity on morphisms *)
    app _ _ _ := id
  |}.
Next Obligation. by cbn; intros. Qed.
Next Obligation. cbn; intros. by rewrite idl. Qed.

(** Functors preserve isomorphisms *)
Lemma app_iso {ð‚ ðƒ} (F: Functor ð‚ ðƒ) A B: A â‰ƒ B -> F A â‰ƒ F B.
Proof.
  (** note how we can also provide the two morphisms from within the proof *)
  intro i. esplit.
  exact (app F (i^1)).
  exact (app F (i^-1)).
Admitted.


(** * 5. Algebras *)

Section algebra.

  (** We fix in this section a category [ð‚] with an endofunctor [F] *)
  Context {ð‚: Category}.
  Variable F: Functor ð‚ ð‚.

  (** F-algebras: an object [A] and a morphism [a: F A ~> A]
      (here [A=alg_car] and [a=alg_mor]) *)
  Record Algebra := alg
    {
      alg_car:> ð‚;
      alg_mor:> F alg_car ~> alg_car
    }.

  (** F-algebra homomorphisms from [(A, a: F A ~> A)] to [(B, b: F B ~> B)]:
      morphisms [f: A ~> B] such that the following square commutes:

       F A ---F f---> F B
        |              |
        a              b
        |              |
        v              v
        A -----f-----> B 

      i.e., f âˆ˜ a â‰¡ b âˆ˜ F f

      below, [f=alg_bod], [a=alg_mor A], [b=alg_mor B], [A=alg_car A], [B=alg_car B]
      we write [F f] as [app F f]
      and we do not need to write [alg_mor] and [alg_car] because they are coercions
   *)
  Record Alg_hom (A B: Algebra) := alg_hom
    {
      alg_bod:> A ~> B;                     (** alg_car A ~> alg_car B *)
      algE: alg_bod âˆ˜ A â‰¡ B âˆ˜ app F alg_bod (** alg_bod âˆ˜ alg_mor A â‰¡ alg_mor B âˆ˜ app F alg_bod *)
    }.
  Arguments alg_bod {_ _}.

  (** The identity algebra homomorphism, from an algebra to itself

       F A ---F id--> F A
        |              |
        a              a
        |              |
        v              v
        A -----id----> A

   *)
  Program Definition alg_id (A: Algebra): Alg_hom A A :=
    {| alg_bod := id |}.
  Next Obligation.
  Admitted.

  (** Composing algebra homomorphisms

       F A ---F f---> F B ---F g---> F C
        |              |              |
        a              b              c
        |              |              |
        v              v              v
        A -----f-----> B -----g-----> C 

   *)
  Program Definition alg_comp (A B C: Algebra)
    (g: Alg_hom B C) (f: Alg_hom A B): Alg_hom A C :=
    {| alg_bod := g âˆ˜ f |}.
  Next Obligation.
  Admitted.

  (** We compare algebra homomorphisms via their underlying morphisms *)
  (** the following line defines [f â‰¡ g] for [f,g : Alg_hom A B] to be
      [alg_bod f â‰¡ alg_bod g], i.e., an equivalence of morphisms in [ð‚] *)
  Canonical Alg_hom_setoid (A B: Algebra) :=
    kern_setoid _ (@alg_bod A B).

  (** F-algebras and their homomorphisms form a category *)
  Program Canonical ALGEBRAS: Category :=
    {| ob := Algebra ; id := @alg_id ; comp := @alg_comp |}.
  Next Obligation. Admitted.
  Next Obligation. Admitted.
  Next Obligation. Admitted.
  Next Obligation. Admitted.

  Section initial_algebra.
    (** an initial algebra is "just" an initial object in the above category of algebras *)
    Context {I: Algebra} (H: initial I).

    (** ** Lambek's lemma: initial F-algebras are fixpoints for F,
        i.e., their underlying morphism actually is an isomorphism *)

    (** shorthand for this morphism *)
    Let i: ð‚ (F I) I := I.

    (** we construct an algebra structure on [F I] *)
    Let FI := alg (F I) (app F i).

    (** by initiality, this yields the backward morphism *)
    Let j: ð‚ I (F I) := H FI.

    (** it remains to prove that they are inverse of each other *)
    Lemma Lambek1: i âˆ˜ j â‰¡ id.
    Proof.
    Admitted.
    Lemma Lambek2: j âˆ˜ i â‰¡ id.
    Proof.
    Admitted.

    (** packing everything together *)
    Definition Lambek: F I â‰ƒ I :=
      {| fwd := i;
         bwd := j;
         isoE := Lambek1;
         isoE' := Lambek2 |}.
  End initial_algebra.

End algebra.
Arguments alg {_ _ _}.


(** * 6. Dually, coalgebras *)
Section coalgebra.

  (** We fix in this section a category [ð‚] with an endofunctor [F]. *)
  Context {ð‚: Category}.
  Variable F: Functor ð‚ ð‚.

  (** F-coalgebras: an object and a morphism *)
  Record Coalgebra := coalg
    {
      coalg_car:> ð‚;
      coalg_mor:> coalg_car ~> F coalg_car
    }.

  (** F-coalgebra homomorphisms: morphisms making the obvious square commute *)
  Record Coalg_hom (A B: Coalgebra) := coalg_hom
    {
      coalg_bod:> A ~> B;
      coalgE: B âˆ˜ coalg_bod â‰¡ app F coalg_bod âˆ˜ A
    }.
  Arguments coalg_bod {_ _}.

  Program Definition coalg_id (A: Coalgebra): Coalg_hom A A :=
    {| coalg_bod := id |}.
  Next Obligation.
  Admitted.

  Program Definition coalg_comp (A B C: Coalgebra)
    (g: Coalg_hom B C) (f: Coalg_hom A B): Coalg_hom A C :=
    {| coalg_bod := g âˆ˜ f |}.
  Next Obligation.
  Admitted.

  (** We compare coalgebra homomorphisms via their underlying morphisms *)
  Canonical Coalg_hom_setoid (A B: Coalgebra) :=
    kern_setoid _ (@coalg_bod A B).

  (** F-coalgebras form a category *)
  Program Canonical Structure COALGEBRAS: Category :=
    {| ob := Coalgebra ; id := @coalg_id ; comp := @coalg_comp |}.
  Next Obligation. Admitted.
  Next Obligation. Admitted.
  Next Obligation. Admitted.
  Next Obligation. Admitted.

  Section final_coalgebra.
    Context {Z: Coalgebra} (H: final Z).

    (** ** CoLambek's lemma: final F-coalgebras are fixpoints for F,
        i.e., their underlying morphism actually is an isomorphism *)

    Lemma CoLambek: F Z â‰ƒ Z.
    Abort.

  End final_coalgebra.

End coalgebra.
Arguments coalg {_ _ _}.
